PREFIX chembl: <http://rdf.ebi.ac.uk/terms/chembl#>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT ?chebi_label (COUNT(?chebi_label) AS ?count)
WHERE {
  ?chembl_comp skos:exactMatch <http://purl.uniprot.org/uniprot/P00918>.
  ?target_uri chembl:hasTargetComponent ?chembl_comp.
  ?assay_uri chembl:hasTarget ?target_uri.
  ?activity chembl:hasAssay ?assay_uri.
  ?activity chembl:hasMolecule ?molecule .
  ?molecule skos:exactMatch ?chebi_compound .
  ?activity chembl:pChembl ?pChembl.
  FILTER (?pChembl > 5).
  {
    ?chebi_compound rdfs:subClassOf ?chebi_class .
    ?chebi_class rdfs:label ?chebi_label .
  }
  UNION
  {
    ?chebi_compound rdfs:subClassOf ?class .
    ?class owl:someValuesFrom ?chebi_class .
    ?chebi_class rdfs:label ?chebi_label .
  }
}
GROUP BY ?chebi_label
