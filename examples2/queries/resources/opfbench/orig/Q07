PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX chembl: <http://rdf.ebi.ac.uk/terms/chembl#>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?target_uri ?assay_uri ?assay_description ?assay_organism ?activity_type ?activity_relation ?activity_value ?compound_uri ?compound_name ?smiles
WHERE {
  GRAPH <http://www.conceptwiki.org> {
    <http://www.conceptwiki.org/concept/ee7e6001-0279-4670-ac05-34bbb050b6e2> skos:exactMatch ?ims_uri .
  }
  GRAPH <http://ims.openphacts.org/> {
    ?target_uri skos:relatedMatch ?ims_uri .
  }
  GRAPH <http://www.ebi.ac.uk/chembl> {
    ?assay_uri chembl:hasTarget ?target_uri .
    ?assay_uri dcterms:description ?assay_description .
    ?assay_uri chembl:organismName ?assay_organism .
    ?assay_uri chembl:hasActivity ?item .
    ?item chembl:pChembl ?pChembl . 
    FILTER (?pChembl > 5).
    ?item chembl:standardType ?activity_type .
    ?item chembl:standardRelation ?activity_relation .
    ?item chembl:standardValue ?standard_value .
    BIND (xsd:decimal(?standard_value) AS ?activity_value) .
    ?item chembl:standardUnits ?activity_unit .
    ?item chembl:hasMolecule ?compound_uri .
  }
  GRAPH <http://ims.openphacts.org/> {
    ?cw_compound skos:exactMatch ?compound_uri .
  }
  GRAPH <http://www.conceptwiki.org> {
    ?cw_compound skos:prefLabel ?compound_name .
  }
  GRAPH <http://ims.openphacts.org/> {
    ?ims_ocrs_compound_uri skos:exactMatch ?compound_uri. 
  }
  GRAPH <http://ops.rsc.org> {
    ?ims_ocrs_compound_uri cheminf:CHEMINF_000018 ?smiles .
  }
}
